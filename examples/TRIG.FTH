\ TRIG.FTH define PI, PI/2, FSIN, FCOS, FTAN, FASIN, FACOS, FATAN, FATAN2
\ Requires LOGS.FTH FSQRT
\ See MATH.FTH for a detailed implementation

ANEW _TRIG_

DECIMAL

  3.1415928E0 2CONSTANT PI
  1.5707964E0 2CONSTANT PI/2

: FCOSI     ( r1 flag -- r2 )
  >R
  PI/2 F/
  2DUP .5E0 F+ F>D
  OVER R> + 3 AND >R
  D>F F- PI/2 F*
  2DUP 2DUP F* FNEGATE 2SWAP
  R> CASE
  0 OF 2DROP  1E0 2SWAP 1 ENDOF
  1 OF FNEGATE    2SWAP 2 ENDOF
  2 OF 2DROP -1E0 2SWAP 1 ENDOF
  3 OF            2SWAP 2 ENDOF
  ENDCASE
  9 SWAP DO
    2OVER 2OVER F*
    I DUP 1+ *
    S>F F/
    2SWAP
  2 +LOOP
  2DROP
  F+ F+ F+ F+ ;

: FSIN      ( r1 -- f2 ) TRUE FCOSI ;

: FCOS      ( r1 -- f2 ) FALSE FCOSI ;

: FTAN      ( r1 -- r2 ) 2DUP FSIN 2SWAP FCOS F/ ;

: FASIN     ( r1 -- r2 )
  1E0 2OVER FABS F< IF -46 THROW THEN
  0.70710678E0 2OVER FABS F< IF
    2DUP F0<
    -ROT
    2DUP F* 1E0 2SWAP F- FSQRT 
    TRUE
  ELSE
    FALSE
  THEN
  -ROT
  2DUP 2DUP 2DUP F* 2SWAP
  34 3 DO
    2OVER F*
    I 2- S>F F*
    I 1- S>F F/
    2DUP I S>F F/
    2ROT 2ROT
  2 +LOOP
  2DROP 2DROP
  16 0 DO F+ LOOP
  ROT IF PI/2 2SWAP F- ROT IF FNEGATE THEN THEN ;

: FACOS     ( r1 -- r2 ) FASIN PI/2 2SWAP F- ;

: FATAN     ( r1 -- r2 )
  1E0 2OVER FABS F< IF
    2DUP F0< -ROT
    1E0 2SWAP FABS F/
    TRUE
  ELSE
    FALSE
  THEN
  -ROT
  .41423562E0 2OVER FABS F< IF
    2DUP 2DUP F* 1E0 F+ FSQRT 1E0 F+ F/
    TRUE
  ELSE
    FALSE
  THEN
  -ROT
  2DUP 2DUP 2DUP F* FNEGATE 2SWAP
  16 3 DO
    2OVER F*
    2DUP I S>F F/
    2ROT 2ROT
  2 +LOOP
  2DROP 2DROP
  F+ F+ F+ F+ F+ F+ F+
  ROT IF 2E0 F* THEN
  ROT IF PI/2 2SWAP F- ROT IF FNEGATE THEN THEN ;

: FATAN2    ( r1 r2 -- r3 )
  2DUP FNEGATE F0< IF
    F/ FATAN
  ELSE
    2SWAP
    2DUP F0= IF
      2DROP F0< IF PI ELSE PI/2 THEN
    ELSE
      PI/2 2OVER F0< IF FNEGATE THEN
      2ROT 2ROT F/ FATAN F-
    THEN
  THEN ;
